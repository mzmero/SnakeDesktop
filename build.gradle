/*
 * This file is part of Snake, licensed under the MIT License (MIT).
 *
 * Copyright (c) 2018 Neolumia
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import com.neolumia.license.header.HeaderStyle

plugins {
  id 'java'
  id 'com.neolumia.license' version '1.0.0'
  id 'com.github.johnrengelman.shadow' version '2.0.3'
}

apply from: 'gradle/idea.gradle'

group = 'com.neolumia'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'

  compile group: 'com.h2database', name: 'h2', version: '1.4.196'
  compile group: 'com.zaxxer', name: 'HikariCP', version: '2.7.8'

  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
  compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.10.0'

  testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourceJar
  archives javadocJar
}

license {
  style = HeaderStyle.BLOCK
  include '**/*.gradle', '**/*.java'
}

jar {
  manifest {
    attributes(
      'Specification-Title': 'Snake',
      'Specification-Version': project.version,
      'Specification-Vendor': 'Pirckheimer-Gymnasium',
      'Main-Class': 'com.neolumia.snake.GameApp',
      'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']})"
    )
  }
}

shadowJar {
  classifier = 'shaded'
}

processResources {
  from 'LICENSE'
}

build.dependsOn(shadowJar)

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
}
